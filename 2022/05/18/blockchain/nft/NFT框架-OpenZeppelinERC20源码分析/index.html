<!DOCTYPE html><html class="appearance-auto" lang="en"><head><meta charset="UTF-8"><title>NFT框架 OpenZeppelin ERC20源码分析</title><meta name="description" content="Blockchain knowledge record blog"><meta name="viewport" content="width=device-width, minimum-scale=1.0, maximum-scale=1.0, user-scalable=no, initial-scale=1"><!-- Google Analytics --><!-- End Google Analytics -->
<!-- Baidu Analytics --><script>var _hmt = _hmt || [];
(function() {
var hm = document.createElement("script");
hm.src = "//hm.baidu.com/hm.js?" + 'e574a92bc4bac2e317309d7ea1dfcb71';
var s = document.getElementsByTagName("script")[0];
s.parentNode.insertBefore(hm, s);
})();</script><!-- End Baidu Analytics --><link rel="icon" href="/images/system/head_portrait.jpg"><link rel="stylesheet" href="/style/common/bulma.css"><link rel="stylesheet" href="/style/base.css"><link rel="stylesheet" href="/style/common/helper.css"><script src="/js/common.js"></script><link rel="stylesheet" href="/style/post.css"><link rel="stylesheet" href="/style/themes/highlight-theme-light.css"><script src="/js/highlight.pack.js"></script><meta name="description" content="一、介绍1. 代码版本release-v4.6
2. 什么是ERC20？ERC20 代币合约跟踪可替代代币：

任何一个代币都完全等同于任何其他代币
没有任何代币具有与之相关的特殊权利或行为

这使得 ERC20 代币可用于交换货币、投票权、质押等媒介
二、源码分析ERC20涉及到的合约列表
Context
IERC20
IERC20Metadata
ERC20

1. Context提供有关当前执行上下文的信息，包括交易的发送者及其数据，虽然可以通过 msg.sender 和 msg.data使用，但是不应直接访问它们，因为在处理元交易时，帐户发送和为执行付费可能不是实际的发送者（就应用程序而言被关注到）
// SPDX-License-Identifier: MIT
// OpenZeppelin Co.."><!-- hexo injector head_end start -->
<link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/katex@0.12.0/dist/katex.min.css">

<link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/hexo-math@4.0.0/dist/style.css">
<!-- hexo injector head_end end --><meta name="generator" content="Hexo 6.1.0">
<style>.github-emoji { position: relative; display: inline-block; width: 1.2em; min-height: 1.2em; overflow: hidden; vertical-align: top; color: transparent; }  .github-emoji > span { position: relative; z-index: 10; }  .github-emoji img, .github-emoji .fancybox { margin: 0 !important; padding: 0 !important; border: none !important; outline: none !important; text-decoration: none !important; user-select: none !important; cursor: auto !important; }  .github-emoji img { height: 1.2em !important; width: 1.2em !important; position: absolute !important; left: 50% !important; top: 50% !important; transform: translate(-50%, -50%) !important; user-select: none !important; cursor: auto !important; } .github-emoji-fallback { color: inherit; } .github-emoji-fallback img { opacity: 0 !important; }</style>
<link rel="alternate" href="/atom.xml" title="Sugar's blog" type="application/atom+xml">
</head><body class="is-flex is-flex-direction-column"><header class="header-widget is-flex-shrink-0 is-hidden-mobile"><div class="container is-fullhd is-flex is-justify-content-space-between is-align-items-center is-full-height"><section class="is-hidden-mobile is-flex-shrink-0"><h2><a href="/">Sugar's blog</a></h2></section><h3 class="is-hidden-mobile is-family-serif is-full-height is-flex is-align-items-center is-flex-shrink-0"><div class="is-full-height" id="postTopic"><p class="is-full-height is-flex-shrink-0 is-flex is-align-items-center is-justify-content-center">NFT框架 OpenZeppelin ERC20源码分析</p><p class="is-full-height is-flex-shrink-0 is-flex is-align-items-center is-justify-content-center">Click back to the top</p></div></h3><aside class="is-flex-shrink-0"><h3 class="is-inline-block"><a href="/">Home</a></h3><h3 class="is-inline-block"><a href="/archives">Archives</a></h3><h3 class="is-inline-block"><a href="/works">Works</a></h3><h3 class="is-inline-block"><a href="/about">About</a></h3></aside></div></header><header class="is-flex header-widget is-flex-shrink-0 is-align-items-center is-justify-content-center is-hidden-tablet"><h3 class="is-inline-block"><a href="/">Home</a></h3><h3 class="is-inline-block"><a href="/archives">Archives</a></h3><h3 class="is-inline-block"><a href="/works">Works</a></h3><h3 class="is-inline-block"><a href="/about">About</a></h3></header><main><main class="container is-max-widescreen content section post-page pt-4 px-4"><div class="columns is-flex-desktop is-justify-content-center is-flex-direction-row-reverse"><div class="column is-3 is-hidden-mobile"><ol class="toc"><li class="toc-item toc-level-1"><a class="toc-link" href="#%E4%B8%80%E3%80%81%E4%BB%8B%E7%BB%8D"><span class="toc-text">一、介绍</span></a><ol class="toc-child"><li class="toc-item toc-level-2"><a class="toc-link" href="#1-%E4%BB%A3%E7%A0%81%E7%89%88%E6%9C%AC"><span class="toc-text">1. 代码版本</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#2-%E4%BB%80%E4%B9%88%E6%98%AFERC20%EF%BC%9F"><span class="toc-text">2. 什么是ERC20？</span></a></li></ol></li><li class="toc-item toc-level-1"><a class="toc-link" href="#%E4%BA%8C%E3%80%81%E6%BA%90%E7%A0%81%E5%88%86%E6%9E%90"><span class="toc-text">二、源码分析</span></a><ol class="toc-child"><li class="toc-item toc-level-2"><a class="toc-link" href="#ERC20%E6%B6%89%E5%8F%8A%E5%88%B0%E7%9A%84%E5%90%88%E7%BA%A6%E5%88%97%E8%A1%A8"><span class="toc-text">ERC20涉及到的合约列表</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#1-Context"><span class="toc-text">1. Context</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#2-IERC20"><span class="toc-text">2. IERC20</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#3-IERC20Metadata"><span class="toc-text">3. IERC20Metadata</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#4-ERC20"><span class="toc-text">4. ERC20</span></a></li></ol></li></ol></div><div class="column is-9"><header class="my-4"><a href="/tags/Ethereum"><i class="tag post-item-tag">Ethereum</i></a><a href="/tags/NFT"><i class="tag post-item-tag">NFT</i></a></header><h1 class="mt-0 mb-1 is-family-serif" id="postTitle">NFT框架 OpenZeppelin ERC20源码分析</h1><time class="has-text-grey" datetime="2022-05-18T02:25:11.000Z">2022-05-18</time><article class="mt-2 post-content"><h1 id="一、介绍"><a href="#一、介绍" class="headerlink" title="一、介绍"></a>一、介绍</h1><h2 id="1-代码版本"><a href="#1-代码版本" class="headerlink" title="1. 代码版本"></a>1. 代码版本</h2><p><a target="_blank" rel="noopener" href="https://github.com/OpenZeppelin/openzeppelin-contracts/tree/release-v4.6">release-v4.6</a></p>
<h2 id="2-什么是ERC20？"><a href="#2-什么是ERC20？" class="headerlink" title="2. 什么是ERC20？"></a>2. 什么是ERC20？</h2><p>ERC20 代币合约跟踪可替代代币：</p>
<ul>
<li>任何一个代币都完全等同于任何其他代币</li>
<li>没有任何代币具有与之相关的特殊权利或行为</li>
</ul>
<p>这使得 ERC20 代币可用于交换货币、投票权、质押等媒介</p>
<h1 id="二、源码分析"><a href="#二、源码分析" class="headerlink" title="二、源码分析"></a>二、源码分析</h1><h2 id="ERC20涉及到的合约列表"><a href="#ERC20涉及到的合约列表" class="headerlink" title="ERC20涉及到的合约列表"></a>ERC20涉及到的合约列表</h2><ul>
<li>Context</li>
<li>IERC20</li>
<li>IERC20Metadata</li>
<li>ERC20</li>
</ul>
<h2 id="1-Context"><a href="#1-Context" class="headerlink" title="1. Context"></a>1. Context</h2><p>提供有关当前执行上下文的信息，包括交易的发送者及其数据，虽然可以通过 msg.sender 和 msg.data使用，但是不应直接访问它们，因为在处理元交易时，帐户发送和为执行付费可能不是实际的发送者（就应用程序而言被关注到）</p>
<pre><code class="solidity">// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
      /**
       * 获取与当前合约交互的地址，该地址可能是用户地址，也可能是合约地址
       */
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

      /**
       * 获取消息data
       */
    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}
</code></pre>
<h2 id="2-IERC20"><a href="#2-IERC20" class="headerlink" title="2. IERC20"></a>2. IERC20</h2><p>EIP 中定义的 ERC20 标准的接口</p>
<pre><code class="javascript">// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev 当 `value` 代币从一个账户 (`from`) 移动到另一个（`to`）触发此事件
     *
     * 请注意，`value` 可能为零
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev 当“owner”的“spender” token通过调用 {approve} 设置时触发此事件
     * `value` 是新的津贴。
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev 返回存在的token数量
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev 返回 `account` 拥有的token数量
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev 将 `amount` 令牌从调用者的帐户移至 `to`。
     *
     * 返回一个布尔值，指示操作是否成功。
     *
     * 发出 {Transfer} 事件。
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev 返回允许“spender”通过 {transferFrom} 代表“owner”花费的token的剩余数量。默认为零。
     *
     * 当 {approve} 或 {transferFrom} 被调用时，这个值会改变。
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets 将 `amount` 设置为 `spender` 在调用者代币上的限额。
     * 返回一个布尔值，指示操作是否成功。
     * 
     * 重要提示：请注意，使用此方法更改配额会带来风险
     * 即有人可能会因不幸的交易排序而同时使用旧配额和新配额
     * 缓解这种竞争条件的一种可能解决方案是首先将支出者的津贴减少到 0，然后设置所需的值：
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729 
     * 
     * 发出 {Approval} 事件
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev 使用allowance机制将指定amount的token从“from”转移到“to”
     * 然后从调用者的allowance中扣除“金额”
     * 
     * 返回一个布尔值，该值指示操作是否成功
     * 
     * 发出 {transferFrom} 事件
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}
</code></pre>
<h2 id="3-IERC20Metadata"><a href="#3-IERC20Metadata" class="headerlink" title="3. IERC20Metadata"></a>3. IERC20Metadata</h2><p>来自 ERC20 标准的可选metadata功能的接口</p>
<pre><code class="solidity">// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev 返回token名字
     */
    function name() external view returns (string memory);

    /**
     * @dev 返回token的标志
     */
    function symbol() external view returns (string memory);

    /**
     * @dev 返回token的小数点位数
     */
    function decimals() external view returns (uint8);
}
</code></pre>
<h2 id="4-ERC20"><a href="#4-ERC20" class="headerlink" title="4. ERC20"></a>4. ERC20</h2><p>ERC-721不可替代令牌标准的可选枚举扩展。</p>
<p>主要方法：</p>
<p>ERC721合约定义了基本的接口方法：</p>
<ul>
<li><p>safeTransferFrom：转移代币所有权</p>
</li>
<li><p>transferFrom：转移代币所有权</p>
</li>
<li><p>setApprovalForAll：授权operator具有所有代币的控制权</p>
</li>
<li><p>balanceOf：返回owner的代币数量</p>
</li>
<li><p>ownerOf：根据tokenId返回代币持有者address</p>
</li>
<li><p>getApproved：查询tokenId的授权人operator address</p>
</li>
<li><p>approve 授权tokenId给地址to</p>
</li>
<li><p>isApprovedForAll：查询一个地址是否为另一个地址的授权操作者。</p>
</li>
<li><p>totalSupply：返回由此契约跟踪的有效nft的计数（代币总量），其中每个nft都有一个分配的、可查询的所有者，且所有者不等于零地址</p>
</li>
<li><p>tokenByIndex：返回第index的NFT的tokenId。没有指定排序顺序。</p>
</li>
<li><p>tokenOfOwnerByIndex：返回分配给指定人的第index的NFT的tokenId。没有指定排序顺序</p>
</li>
</ul>
<p>源码：</p>
<pre><code class="solidity">// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/ERC20.sol)

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev {IERC20} 接口的实现
 *
 * 此实现与创建令牌的方式无关
 * 这意味着必须使用 {_mint} 在派生合约中添加供应机制。
 * 有关通用机制，请参阅 {ERC20PresetMinterPauser}
 * 
 * 提示：有关详细说明，请参阅我们的指南 https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[如何实施供应机制]
 * 
 * 我们遵循了一般的 OpenZeppelin Contracts 指南：
 * 函数在失败时恢复而不是返回 `false`。这种行为仍然是传统的，并且与 ERC20 应用程序的期望不冲突
 * 此外，在调用 {transferFrom} 时会发出 {Approval} 事件。这允许应用程序仅通过收听所述事件来重建所有帐户的限额。 EIP 的其他实现可能不会发出这些事件，因为规范没有要求。
 * 最后，添加了非标准的 {decreaseAllowance} 和 {increaseAllowance} 函数，以缓解围绕设置限额的众所周知的问题。请参阅 {IERC20-approve}
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping(address =&gt; uint256) private _balances;

    mapping(address =&gt; mapping(address =&gt; uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev 设置 {name} 和 {symbol}.
     *
     * {decimals} 的默认值为 18。要为 {decimals} 选择不同的值，应重载它
     * 
     * 这两个值都是不可变的：只能在构造过程中设置一次
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev 返回token的名字
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev 返回token的标志，通常是比名字短一点的缩写
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev 返回用于获取其用户表示的小数位数
     * 例如，如果“decimals”等于“2”，则“505”代币的余额应显示为“5.05”（“505 / 10 ** 2”）
     * 代币通常选择 18 的值，模仿 Ether 和 Wei 之间的关系。这是 {ERC20} 使用的值，除非此函数被覆盖；
     
     * 注意：此信息仅用于_显示_目的：它绝不会影响合约的任何算术，包括 {IERC20-balanceOf} 和 {IERC20-transfer}
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * 规定:
     *
     * - `to` 不能是零地址.
     * - 调用者至少有`amount`数量的token
     */
    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _transfer(owner, to, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * 注意：如果 `amount` 是最大值 `uint256`，则不会在 `transferFrom` 上更新配额。这在语义上等同于无限批准
     *
     * 规定:
     *
     * - `spender` 不能是零地址.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * 发出一个 {Approval} 事件，指示更新的配额
     * EIP 不需要这样做。请参阅 {ERC20} 开头的注释
     * 
     * 注意：如果当前allowance是最大的 `uint256`，则不会更新限额
     *
     * 规定:
     *
     * - `from` 和 `to` 不能是零地址.
     * - `from` 至少有`amount`数量的token
     * - 调用者从`from`获取到的allowance至少是`amount`.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) public virtual override returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, amount);
        _transfer(from, to, amount);
        return true;
    }

    /**
     * @dev 以原子方式增加调用者授予 `spender` 的allowance。
     *
     * 这是 {approve} 的替代方案，可用作缓解
     * {IERC20-approve} 中描述的问题
     *
     * 发出一个 {Approval} 事件，指示更新的allowance
     * 
     * 规定:
     *
     * - `spender` 不能是零地址.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, allowance(owner, spender) + addedValue);
        return true;
    }

    /**
     * @dev 以原子方式减少调用者授予 `spender` 的allowance。
     *
     * 这是 {approve} 的替代方案，可用作缓解
     * {IERC20-approve} 中描述的问题。
     *
     * 发出一个 {Approval} 事件，指示更新的allowance。
     * 
     * 规定:
     *
     * - `spender` 不能是零地址.
     * - `spender` 至少有来自调用者授予的subtractedValue数量的allowance
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        address owner = _msgSender();
        uint256 currentAllowance = allowance(owner, spender);
        require(currentAllowance &gt;= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(owner, spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev 将令牌的“数量”从“from”移动到“to”。
     *
     * 这个内部函数等价于{transfer}，可用于
     * 例如实施自动代币费用，削减机制等。
     *
     * 发出 {Transfer} 事件
     *
     * 规定:
     *
     * - `from` 不能是零地址.
     * - `to` 不能是零地址.
     * - `from` 的余额至少 `amount`
     */
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(from, to, amount);

        uint256 fromBalance = _balances[from];
        require(fromBalance &gt;= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[from] = fromBalance - amount;
        }
        _balances[to] += amount;

        emit Transfer(from, to, amount);

        _afterTokenTransfer(from, to, amount);
    }

    /** @dev 创建 `amount` 代币并将它们分配给 `account`，增加总供应量。
     *
     * 发出一个 {Transfer} 事件，并将 `from` 设置为零地址。
       * 
     * 规定:
     *
     * - `account` 不能是零地址.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        _balances[account] += amount;
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }

    /**
     * @dev 销毁 `account` 中的 `amount` 代币，减少总供应量。
     *
     * 发出一个 {Transfer} 事件，并将 `to` 设置为零地址。
     Destroys `amount` tokens from `account`, reducing the
     *
     * 规定:
     *
     * - `account` 不能是零地址
     * - `account` 至少有`amount`个token
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance &gt;= amount, "ERC20: burn amount exceeds balance");
        unchecked {
            _balances[account] = accountBalance - amount;
        }
        _totalSupply -= amount;

        emit Transfer(account, address(0), amount);

        _afterTokenTransfer(account, address(0), amount);
    }

    /**
     * @dev 将 `amount` 设置为 `spender` 在 `owner` 的代币上的限额。
     *
     * 这个内部函数相当于`approve`，可以用来
     * 例如为某些子系统设置自动限额等。
     *
     * 发出 {Approval} 事件
     * 
     * 规定:
     *
     * - `owner` 不能是零地址.
     * - `spender` 不能是零地址.
     */
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev 根据花费的“amount”更新“owner”对“spender”的津贴
     *
     * 在无限津贴的情况下不更新津贴金额
     * 如果没有足够的配额，请恢复
     *
     * 可能会发出 {Approval} 事件。
     */
    function _spendAllowance(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        uint256 currentAllowance = allowance(owner, spender);
        if (currentAllowance != type(uint256).max) {
            require(currentAllowance &gt;= amount, "ERC20: insufficient allowance");
            unchecked {
                _approve(owner, spender, currentAllowance - amount);
            }
        }
    }

    /**
     * @dev 在任何token转移之前调用的Hook
     * 这包括mint和burn
     * 
     * 调用条件:
     *
     * - 当 `from` 和 `to` 都非零时，`from` 的amount数量的token将被转移到`to`
     * - 当 `from` 为零时，将为 `to` 铸造 `amount` 数量的token
     * - 当 `to` 为 0 时，`from` 的`amount` 数量的token将被烧毁
     * - `from` 和 `to` 永远不会都是零
     *
     * 要了解有关挂钩的更多信息，请访问：ROOT:extending-contracts.adoc#using-hooks[Using Hooks]
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}

    /**
     * @dev 在任何token转移后调用的Hook
     * 这包括mint和burn
     *
     * 调用条件：
     *
     * - 当 `from` 和 `to` 都非零时，`from` 的`amount`数量的token已转移到`to`
     * - 当 `from` 为零时，为 `to` 铸造 `amount` 数量的token
     * - 当 `to` 为 0 时，`from` 的`amount`数量的token已被烧毁
     * - `from` 和 `to` 永远不会都是零
     * 
     * 要了解有关挂钩的更多信息，请访问：ROOT:extending-contracts.adoc#using-hooks[Using Hooks]
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}
}
</code></pre>
</article><section class="jump-container is-flex is-justify-content-space-between my-6"><!-- em is empty placeholder--><a class="button is-default" href="/2022/05/19/blockchain/nft/NFT%E6%A1%86%E6%9E%B6-OpenZeppelinERC721%E6%BA%90%E7%A0%81%E5%88%86%E6%9E%90/" title="NFT框架 OpenZeppelin ERC721源码分析"><i class="iconfont icon-prev mr-2 has-text-grey"></i><span class="has-text-weight-semibold">Previous: NFT框架 OpenZeppelin ERC721源码分析</span></a><a class="button is-default" href="/2022/05/17/blockchain/ethereum/Solidity%E5%AD%A6%E4%B9%A0%E7%AC%94%E8%AE%B0(%E6%94%BB%E5%87%BB)/" title="Solidity学习笔记(攻击)"><span class="has-text-weight-semibold">Next: Solidity学习笔记(攻击)</span><i class="iconfont icon-next ml-2 has-text-grey"></i></a></section><article class="mt-6 comment-container"><script async repo="zhangzhishun/blockchain-blog-comment" src="https://utteranc.es/client.js" issue-term="pathname" theme="preferred-color-scheme"></script></article></div></div></main></main><footer class="is-flex is-flex-direction-column is-align-items-center is-flex-shrink-0 is-family-serif"><section class="sns-container"><a title="twitter" target="_blank" rel="noopener nofollow" href="//twitter.com/zhang_zhishun"><i class="iconfont icon-twitter"></i></a><!-- Github--><a title="github" target="_blank" rel="noopener nofollow" href="//github.com/zhangzhishun"><i class="iconfont icon-github"></i></a><!-- Ins--><!-- RSS--><a title="rss" target="_blank" rel="noopener nofollow" href="/atom.xml"><i class="iconfont icon-rss"></i></a><!-- 知乎--><!-- 领英--><!-- 脸书--></section><p><span>Copyright ©</span><span> Sugar 2022</span></p><div><span></span></div></footer><script async defer src="https://buttons.github.io/buttons.js"></script><script src="/js/post.js"></script></body></html>