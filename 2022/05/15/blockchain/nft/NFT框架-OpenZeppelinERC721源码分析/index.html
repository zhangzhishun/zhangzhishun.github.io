<!DOCTYPE html><html class="appearance-auto" lang="en"><head><meta charset="UTF-8"><title>NFT框架 OpenZeppelin ERC721源码分析</title><meta name="robots" content="noindex"><meta name="description" content="Blockchain knowledge record blog"><meta name="viewport" content="width=device-width, minimum-scale=1.0, maximum-scale=1.0, user-scalable=no, initial-scale=1"><!-- Google Analytics --><!-- End Google Analytics -->
<!-- Baidu Analytics --><script>var _hmt = _hmt || [];
(function() {
var hm = document.createElement("script");
hm.src = "//hm.baidu.com/hm.js?" + 'e574a92bc4bac2e317309d7ea1dfcb71';
var s = document.getElementsByTagName("script")[0];
s.parentNode.insertBefore(hm, s);
})();</script><!-- End Baidu Analytics --><link rel="icon" href="/images/system/head_portrait.jpg"><link rel="stylesheet" href="/style/common/bulma.css"><link rel="stylesheet" href="/style/base.css"><link rel="stylesheet" href="/style/common/helper.css"><script src="/js/common.js"></script><link rel="stylesheet" href="/style/post.css"><link rel="stylesheet" href="/style/themes/highlight-theme-light.css"><script src="/js/highlight.pack.js"></script><meta name="description" content="一、介绍1. 代码版本release-v4.6
2. 什么是ERC721？ERC-721 为 NFT 引入了一个标准，换言之， 这种类型的代币是独一无二的
二、源码分析ERC721涉及到的合约列表
Context
IERC165、ERC165
IERC721Metadata
IERC721
ERC721

1. Context参考： NFT框架 OpenZeppelin ERC20源码分析
2. IERC165、ERC165// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev ERC165 .."><!-- hexo injector head_end start -->
<link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/katex@0.12.0/dist/katex.min.css">

<link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/hexo-math@4.0.0/dist/style.css">
<!-- hexo injector head_end end --><meta name="generator" content="Hexo 6.1.0">
<style>.github-emoji { position: relative; display: inline-block; width: 1.2em; min-height: 1.2em; overflow: hidden; vertical-align: top; color: transparent; }  .github-emoji > span { position: relative; z-index: 10; }  .github-emoji img, .github-emoji .fancybox { margin: 0 !important; padding: 0 !important; border: none !important; outline: none !important; text-decoration: none !important; user-select: none !important; cursor: auto !important; }  .github-emoji img { height: 1.2em !important; width: 1.2em !important; position: absolute !important; left: 50% !important; top: 50% !important; transform: translate(-50%, -50%) !important; user-select: none !important; cursor: auto !important; } .github-emoji-fallback { color: inherit; } .github-emoji-fallback img { opacity: 0 !important; }</style>
<link rel="alternate" href="/atom.xml" title="Sugar's blog" type="application/atom+xml">
</head><body class="is-flex is-flex-direction-column"><header class="header-widget is-flex-shrink-0 is-hidden-mobile"><div class="container is-fullhd is-flex is-justify-content-space-between is-align-items-center is-full-height"><section class="is-hidden-mobile is-flex-shrink-0"><h2><a href="/">Sugar's blog</a></h2></section><h3 class="is-hidden-mobile is-family-serif is-full-height is-flex is-align-items-center is-flex-shrink-0"><div class="is-full-height" id="postTopic"><p class="is-full-height is-flex-shrink-0 is-flex is-align-items-center is-justify-content-center">NFT框架 OpenZeppelin ERC721源码分析</p><p class="is-full-height is-flex-shrink-0 is-flex is-align-items-center is-justify-content-center">Click back to the top</p></div></h3><aside class="is-flex-shrink-0"><h3 class="is-inline-block"><a href="/">Home</a></h3><h3 class="is-inline-block"><a href="/archives">Archives</a></h3><h3 class="is-inline-block"><a href="/works">Works</a></h3><h3 class="is-inline-block"><a href="/about">About</a></h3></aside></div></header><header class="is-flex header-widget is-flex-shrink-0 is-align-items-center is-justify-content-center is-hidden-tablet"><h3 class="is-inline-block"><a href="/">Home</a></h3><h3 class="is-inline-block"><a href="/archives">Archives</a></h3><h3 class="is-inline-block"><a href="/works">Works</a></h3><h3 class="is-inline-block"><a href="/about">About</a></h3></header><main><main class="container is-max-widescreen content section post-page pt-4 px-4"><div class="columns is-flex-desktop is-justify-content-center is-flex-direction-row-reverse"><div class="column is-3 is-hidden-mobile"><ol class="toc"><li class="toc-item toc-level-1"><a class="toc-link" href="#%E4%B8%80%E3%80%81%E4%BB%8B%E7%BB%8D"><span class="toc-text">一、介绍</span></a><ol class="toc-child"><li class="toc-item toc-level-2"><a class="toc-link" href="#1-%E4%BB%A3%E7%A0%81%E7%89%88%E6%9C%AC"><span class="toc-text">1. 代码版本</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#2-%E4%BB%80%E4%B9%88%E6%98%AFERC721%EF%BC%9F"><span class="toc-text">2. 什么是ERC721？</span></a></li></ol></li><li class="toc-item toc-level-1"><a class="toc-link" href="#%E4%BA%8C%E3%80%81%E6%BA%90%E7%A0%81%E5%88%86%E6%9E%90"><span class="toc-text">二、源码分析</span></a><ol class="toc-child"><li class="toc-item toc-level-2"><a class="toc-link" href="#ERC721%E6%B6%89%E5%8F%8A%E5%88%B0%E7%9A%84%E5%90%88%E7%BA%A6%E5%88%97%E8%A1%A8"><span class="toc-text">ERC721涉及到的合约列表</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#1-Context"><span class="toc-text">1. Context</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#2-IERC165%E3%80%81ERC165"><span class="toc-text">2. IERC165、ERC165</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#3-IERC721Metadata"><span class="toc-text">3. IERC721Metadata</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#4-IERC721"><span class="toc-text">4. IERC721</span></a></li><li class="toc-item toc-level-2"><a class="toc-link" href="#5-ERC721"><span class="toc-text">5. ERC721</span></a></li></ol></li></ol></div><div class="column is-9"><header class="my-4"><a href="/tags/NFT"><i class="tag post-item-tag">NFT</i></a><a href="/tags/OpenZeppelin"><i class="tag post-item-tag">OpenZeppelin</i></a></header><h1 class="mt-0 mb-1 is-family-serif" id="postTitle">NFT框架 OpenZeppelin ERC721源码分析</h1><time class="has-text-grey" datetime="2022-05-15T02:25:11.000Z">2022-05-15</time><article class="mt-2 post-content"><h1 id="一、介绍"><a href="#一、介绍" class="headerlink" title="一、介绍"></a>一、介绍</h1><h2 id="1-代码版本"><a href="#1-代码版本" class="headerlink" title="1. 代码版本"></a>1. 代码版本</h2><p><a target="_blank" rel="noopener" href="https://github.com/OpenZeppelin/openzeppelin-contracts/tree/release-v4.6">release-v4.6</a></p>
<h2 id="2-什么是ERC721？"><a href="#2-什么是ERC721？" class="headerlink" title="2. 什么是ERC721？"></a>2. 什么是ERC721？</h2><p>ERC-721 为 NFT 引入了一个标准，换言之， 这种类型的代币是独一无二的</p>
<h1 id="二、源码分析"><a href="#二、源码分析" class="headerlink" title="二、源码分析"></a>二、源码分析</h1><h2 id="ERC721涉及到的合约列表"><a href="#ERC721涉及到的合约列表" class="headerlink" title="ERC721涉及到的合约列表"></a>ERC721涉及到的合约列表</h2><ul>
<li>Context</li>
<li>IERC165、ERC165</li>
<li>IERC721Metadata</li>
<li>IERC721</li>
<li>ERC721</li>
</ul>
<h2 id="1-Context"><a href="#1-Context" class="headerlink" title="1. Context"></a>1. Context</h2><p>参考： <a href="/2022/05/10/blockchain/nft/NFT%E6%A1%86%E6%9E%B6-OpenZeppelinERC20%E6%BA%90%E7%A0%81%E5%88%86%E6%9E%90/" title="NFT框架 OpenZeppelin ERC20源码分析">NFT框架 OpenZeppelin ERC20源码分析</a></p>
<h2 id="2-IERC165、ERC165"><a href="#2-IERC165、ERC165" class="headerlink" title="2. IERC165、ERC165"></a>2. IERC165、ERC165</h2><pre><code class="solidity">// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev ERC165 标准的接口，如https://eips.ethereum.org/EIPS/eip-165[EIP]
 *
 * 实现者可以声明对合约接口的支持，然后可以被其他人查询（{ERC165Checker}）
 *
 * 有关实现，请参阅 {ERC165}
 */
interface IERC165 {
    /**
     * @dev 如果此合约通过`interfaceId`实现了定义的接口，则返回 true
     * 见对应：https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP部分]了解有关如何创建这些 ID 的更多信息。
     *
     * 此函数调用必须使用少于 30 000 个气体
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev {IERC165}接口的实现。
 *
 * 想要实现 ERC165 的合约应该从这个合约继承并覆盖 {supportsInterface} 来检查用于将支持的附加接口 ID。例如：
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * 或者，{ERC165Storage} 提供了一种更易于使用但更昂贵的实现方式
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}
</code></pre>
<h2 id="3-IERC721Metadata"><a href="#3-IERC721Metadata" class="headerlink" title="3. IERC721Metadata"></a>3. IERC721Metadata</h2><pre><code class="solidity">// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/extensions/IERC721Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC721.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional metadata extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721Metadata is IERC721 {
    /**
     * @dev Returns the token collection name.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) external view returns (string memory);
}
</code></pre>
<h2 id="4-IERC721"><a href="#4-IERC721" class="headerlink" title="4. IERC721"></a>4. IERC721</h2><pre><code class="solidity">// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev 符合 ERC721 的合约所需的接口
 */
interface IERC721 is IERC165 {
    /**
     * @dev 当 `tokenId` 从 `from` 转移到 `to` 时发出
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev 当 `owner` 启用 `approved` 来管理 `tokenId` 令牌时发出
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev 当 `owner` 启用或禁用 `operator` 以管理其所有资产时发出
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev 返回“所有者”帐户中的代币数量
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev 返回 `tokenId` 的所有者
     *
     * 要求:
     *
     * - `tokenId` 必须存在.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev 安全地将 `tokenId` 从 `from` 转移到 `to`
     *
     * 要求:
     *
     * - `from` 不能是零地址
     * - `to` 不能是零地址
     * - `tokenId` 必须存在且属于owner
     * - 如果调用者不是“from”，则必须通过 {approve} 或 {setApprovalForAll} 批准交易此token
     * - 如果 `to` 指的是智能合约，它必须实现 {IERC721Receiver-onERC721Received}，这是在安全传输时调用的。
     *
     * 触发 {Transfer} 事件
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;

    /**
     * @dev 安全地将 `tokenId` 从 `from` 转移到 `to`，首先检查合约接收者是否了解 ERC721 协议，以防止代币被永久锁定
     *
     * 要求:
     *
     * - `from` 不能是零地址
     * - `to` 不能是零地址
     * - `tokenId` 必须存在且属于owner
     * - 如果调用者不是 `from`，则必须已被 {approve} 或 {setApprovalForAll} 允许交易此token
     * - 如果 `to` 指的是智能合约，它必须实现 {IERC721Receiver-onERC721Received}，这是在安全传输时调用的。
     *
     * 触发 {Transfer} 事件
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev 将 `tokenId` 从 `from` 转移到 `to`
     *
     * 警告：不鼓励使用此方法，尽可能使用 {safeTransferFrom}
     *
     * Requirements:
     *
     * - `from` 不能是零地址
     * - `to` 不能是零地址
     * - `tokenId` 必须存在且属于owner
     * - 如果调用者不是“from”，则必须通过 {approve} 或 {setApprovalForAll} 批准交易此token
     *
     * 触发 {Transfer} 事件
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev 授予 `to` 将 `tokenId` 令牌转移到另一个帐户的权限。
     * 转账token后清除审批
     *
     * 一次只能批准一个账户，所以批准零地址会清除之前的批准
     *
     * 要求:
     *
     * - 调用者必须拥有令牌或者是经过批准的操作人
     * - `tokenId` 必须存在
     *
     * 触发 {Approval} 事件
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev 批准或删除 `operator` 作为调用者的操作人
     * 操作人可以为调用者拥有的任何token调用 {transferFrom} 或 {safeTransferFrom}。
     *
     * 要求:
     *
     * - `operator` 不能是调用者
     *
     * 触发 {ApprovalForAll} 事件
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev 返回为 `tokenId` 批准的帐户
     *
     * 要求:
     *
     * - `tokenId` 必须存在
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev 如果允许 `operator` 管理 `owner` 的所有资产，则返回true
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);
}
</code></pre>
<h2 id="5-ERC721"><a href="#5-ERC721" class="headerlink" title="5. ERC721"></a>5. ERC721</h2><pre><code class="solidity">// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC721/ERC721.sol)

pragma solidity ^0.8.0;

import "./IERC721.sol";
import "./IERC721Receiver.sol";
import "./extensions/IERC721Metadata.sol";
import "../../utils/Address.sol";
import "../../utils/Context.sol";
import "../../utils/Strings.sol";
import "../../utils/introspection/ERC165.sol";

/**
 * @dev https://eips.ethereum.org/EIPS/eip-721[ERC721] Non-Fungible Token Standard 的实现
 * 包括Metadata 扩展，但不包括 Enumerable 扩展，可单独提供{ERC721Enumerable}
 */
contract ERC721 is Context, ERC165, IERC721, IERC721Metadata {
    using Address for address;
    using Strings for uint256;

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    // Mapping from token ID to owner address
    mapping(uint256 =&gt; address) private _owners;

    // Mapping owner address to token count
    mapping(address =&gt; uint256) private _balances;

    // Mapping from token ID to approved address
    mapping(uint256 =&gt; address) private _tokenApprovals;

    // Mapping from owner to operator approvals
    mapping(address =&gt; mapping(address =&gt; bool)) private _operatorApprovals;

    /**
     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC721).interfaceId ||
            interfaceId == type(IERC721Metadata).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC721-balanceOf}.
     */
    function balanceOf(address owner) public view virtual override returns (uint256) {
        require(owner != address(0), "ERC721: balance query for the zero address");
        return _balances[owner];
    }

    /**
     * @dev See {IERC721-ownerOf}.
     */
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        address owner = _owners[tokenId];
        require(owner != address(0), "ERC721: owner query for nonexistent token");
        return owner;
    }

    /**
     * @dev See {IERC721Metadata-name}.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev See {IERC721Metadata-symbol}.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev See {IERC721Metadata-tokenURI}.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "ERC721Metadata: URI query for nonexistent token");

        string memory baseURI = _baseURI();
        return bytes(baseURI).length &gt; 0 ? string(abi.encodePacked(baseURI, tokenId.toString())) : "";
    }

    /**
     * @dev 用于计算 {tokenURI} 的基本 URI。如果设置，则为每个token生成的 URI 将是 `baseURI` 和 `tokenId` 的拼接
     * 默认情况下是空的，可以在子合约中被覆盖
     */
    function _baseURI() internal view virtual returns (string memory) {
        return "";
    }

    /**
     * @dev See {IERC721-approve}.
     */
    function approve(address to, uint256 tokenId) public virtual override {
        address owner = ERC721.ownerOf(tokenId);
        require(to != owner, "ERC721: approval to current owner");

        require(
            _msgSender() == owner || isApprovedForAll(owner, _msgSender()),
            "ERC721: approve caller is not owner nor approved for all"
        );

        _approve(to, tokenId);
    }

    /**
     * @dev See {IERC721-getApproved}.
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        require(_exists(tokenId), "ERC721: approved query for nonexistent token");

        return _tokenApprovals[tokenId];
    }

    /**
     * @dev See {IERC721-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC721-isApprovedForAll}.
     */
    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    /**
     * @dev See {IERC721-transferFrom}.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        //solhint-disable-next-line max-line-length
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");

        _transfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        safeTransferFrom(from, to, tokenId, "");
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) public virtual override {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");
        _safeTransfer(from, to, tokenId, data);
    }

    /**
     * @dev 安全地将 `tokenId` 令牌从 `from` 转移到 `to`，首先检查合同接收者
     * 了解 ERC721 协议以防止令牌被永久锁定。
     *
     * `data` 是附加数据，没有指定格式，在调用过程中给 `to` 发送。
     *
     * 此内部函数等价于 {safeTransferFrom}，可用于例如实现替代机制来执行令牌传输，例如基于签名的
     *
     *
     * 要求:
     *
     * - `from` 不能是零地址
     * - `to` 不能是零地址
     * - `tokenId` 必须存在且属于owner
     * - 如果 `to` 指的是智能合约，它必须实现 {IERC721Receiver-onERC721Received}，这是在安全传输时调用的。
     */
    function _safeTransfer(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) internal virtual {
        _transfer(from, to, tokenId);
        require(_checkOnERC721Received(from, to, tokenId, data), "ERC721: transfer to non ERC721Receiver implementer");
    }

    /**
     * @dev 返回 `tokenId` 是否存在
     *
     * token可以由其所有者或通过 {approve} 或 {setApprovalForAll} 批准的帐户管理
     *
     * token在铸造时开始存在（`_mint`），并在它们被烧毁时停止存在（`_burn`）
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return _owners[tokenId] != address(0);
    }

    /**
     * @dev 返回是否允许 `spender` 管理 `tokenId`
     *
     * 要求:
     *
     * - `tokenId` 必须存在
     */
    function _isApprovedOrOwner(address spender, uint256 tokenId) internal view virtual returns (bool) {
        require(_exists(tokenId), "ERC721: operator query for nonexistent token");
        address owner = ERC721.ownerOf(tokenId);
        return (spender == owner || isApprovedForAll(owner, spender) || getApproved(tokenId) == spender);
    }

    /**
     * @dev 安全地铸造 `tokenId` 并将其转移到 `to`
     *
     * 要求:
     *
     * - `tokenId` 必须不存在
     * - 如果 `to` 指的是智能合约，它必须实现 {IERC721Receiver-onERC721Received}，这是在安全传输时调用的。
     *
     * 触发 {Transfer} 事件.
     */
    function _safeMint(address to, uint256 tokenId) internal virtual {
        _safeMint(to, tokenId, "");
    }

    /**
     * @dev 与 {xref-ERC721-_safeMint-address-uint256-}[`_safeMint`] 相同，带有一个额外的 `data` 参数，即在 {IERC721Receiver-onERC721Received} 中转发给合约的收货人
     */
    function _safeMint(
        address to,
        uint256 tokenId,
        bytes memory data
    ) internal virtual {
        _mint(to, tokenId);
        require(
            _checkOnERC721Received(address(0), to, tokenId, data),
            "ERC721: transfer to non ERC721Receiver implementer"
        );
    }

    /**
     * @dev 铸造 `tokenId` 并将其转移到 `to`
     *
     * 警告：不鼓励使用此方法，尽可能使用 {_safeMint}
     *
     * 要求:
     *
     * - `tokenId` 必须不存在
     * - `to` 不能是零地址
     *
     * 触发 {Transfer} 事件
     */
    function _mint(address to, uint256 tokenId) internal virtual {
        require(to != address(0), "ERC721: mint to the zero address");
        require(!_exists(tokenId), "ERC721: token already minted");

        _beforeTokenTransfer(address(0), to, tokenId);

        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(address(0), to, tokenId);

        _afterTokenTransfer(address(0), to, tokenId);
    }

    /**
     * @dev 销毁`tokenId`
     * 销毁令牌时清除批准
     * 
     * 要求:
     *
     * - `tokenId` 必须存在
     *
     * 触发 {Transfer} 事件.
     */
    function _burn(uint256 tokenId) internal virtual {
        address owner = ERC721.ownerOf(tokenId);

        _beforeTokenTransfer(owner, address(0), tokenId);

        // Clear approvals
        _approve(address(0), tokenId);

        _balances[owner] -= 1;
        delete _owners[tokenId];

        emit Transfer(owner, address(0), tokenId);

        _afterTokenTransfer(owner, address(0), tokenId);
    }

    /**
     * @dev 将 `tokenId` 从 `from` 转移到 `to`
     * 与 {transferFrom} 不同，这对 msg.sender 没有任何限制
     *
     * 要求:
     *
     * - `to` 不能是零地址
     * - `tokenId` 必须属于 `from`
     *
     * 触发 {Transfer} 事件.
     */
    function _transfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {
        require(ERC721.ownerOf(tokenId) == from, "ERC721: transfer from incorrect owner");
        require(to != address(0), "ERC721: transfer to the zero address");

        _beforeTokenTransfer(from, to, tokenId);

        // Clear approvals from the previous owner
        _approve(address(0), tokenId);

        _balances[from] -= 1;
        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);

        _afterTokenTransfer(from, to, tokenId);
    }

    /**
     * @dev 批准 `to` 对 `tokenId` 进行操作
     *
     * 触发 {Approval} 事件
     */
    function _approve(address to, uint256 tokenId) internal virtual {
        _tokenApprovals[tokenId] = to;
        emit Approval(ERC721.ownerOf(tokenId), to, tokenId);
    }

    /**
     * @dev 批准 `operator` 对所有 `owner` token进行操作
     *
     * 触发 {ApprovalForAll} 事件.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC721: approve to caller");
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    /**
     * @dev 在目标地址上调用 {IERC721Receiver-onERC721Received} 的内部函数。
     * 如果目标地址不是合约，则不执行调用。
     *
     * @param from address representing the previous owner of the given token ID
     * @param to target address that will receive the tokens
     * @param tokenId uint256 ID of the token to be transferred
     * @param data bytes optional data to send along with the call
     * @return bool whether the call correctly returned the expected magic value
     */
    function _checkOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) private returns (bool) {
        if (to.isContract()) {
            try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, data) returns (bytes4 retval) {
                return retval == IERC721Receiver.onERC721Received.selector;
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert("ERC721: transfer to non ERC721Receiver implementer");
                } else {
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        } else {
            return true;
        }
    }

    /**
     * @dev 在任何令牌传输之前调用的钩子。这包括铸币和销毁
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, ``from``'s `tokenId` will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}

    /**
     * @dev 在任何代币转移后调用的挂钩。这包括铸造和销毁
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}
}
</code></pre>
<!-- flag of hidden posts --></article><section class="jump-container is-flex is-justify-content-space-between my-6"><!-- em is empty placeholder--><em></em></section><article class="mt-6 comment-container"><script async repo="zhangzhishun/blockchain-blog-comment" src="https://utteranc.es/client.js" issue-term="pathname" theme="preferred-color-scheme"></script></article></div></div></main></main><footer class="is-flex is-flex-direction-column is-align-items-center is-flex-shrink-0 is-family-serif"><section class="sns-container"><a title="twitter" target="_blank" rel="noopener nofollow" href="//twitter.com/zhang_zhishun"><i class="iconfont icon-twitter"></i></a><!-- Github--><a title="github" target="_blank" rel="noopener nofollow" href="//github.com/zhangzhishun"><i class="iconfont icon-github"></i></a><!-- Ins--><!-- RSS--><a title="rss" target="_blank" rel="noopener nofollow" href="/atom.xml"><i class="iconfont icon-rss"></i></a><!-- 知乎--><!-- 领英--><!-- 脸书--></section><p><span>Copyright ©</span><span> Sugar 2022</span></p><div><span></span></div></footer><script async defer src="https://buttons.github.io/buttons.js"></script><script src="/js/post.js"></script></body></html>